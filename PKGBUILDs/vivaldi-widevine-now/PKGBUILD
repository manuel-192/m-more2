# Maintainer: Doug Newgard <scimmia at archlinux dot org>

# Another maintainer: manuel (see forum.endeavouros.com)

# Chrome info:
_channel=stable
_chrome_ver=86.0.4240.193
_chrome_bin=google-chrome-${_channel}_${_chrome_ver}-1_amd64.deb

pkgname=vivaldi-widevine-now
pkgdesc='A browser plugin designed for the viewing of premium video content'
pkgver=4.10.1679.0
pkgrel=1
epoch=1
arch=('x86_64')
#_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
_url="https://raw.githubusercontent.com/manuel-192/m-more2/master/PKGBUILDs/$pkgname"
url='https://www.widevine.com/'
license=('custom')
provides=("vivaldi-widevine")
conflicts=('vivaldi-widevine' 'google-chrome')
options=('!strip')
source=(
  "https://dl.google.com/linux/deb/pool/main/g/google-chrome-${_channel}/${_chrome_bin}"
  $_url/update_chrome_version.sh
)
sha256sums=('fc4a4717ddf9623eeb2ebd4cf0f3a845a20fab34bb2831af2a264507b6432ef4'
            'd88e4e6ef446cf4fbd5f7ccab0b6d47b86a89634e0410fabb2174b13748c5866')
sha512sums=('a1e33f91a67afd9b724f8d31840613a8754f8f8de33aa95e3c59c4797d47de3fb207b3e404d5abe4e9f81e7fd0503e39f616264a159fec82200e857e900120e0'
            '81f9718b378f69df7ad2e38927c2e5cdf185863ac95a511dc986ca8679144879ae9cef86f7f83a97f98814f79d1b9c0cc7c6991f888eaee6c6967aa2fc63a344')

prepare() {
  bsdtar -x --strip-components 4 -f data.tar.xz opt/google/chrome/WidevineCdm
}

pkgver() {
  grep '"version":' WidevineCdm/manifest.json | sed -e 's|^.*"version": "\([0-9\.]*\)",$|\1|'
}

package() {
  depends=('gcc-libs' 'glib2' 'glibc' 'nspr' 'nss')

  local targetdir=opt/google/chrome

  install -dm755 "$pkgdir/$targetdir"
  cp -a WidevineCdm "$pkgdir/$targetdir/"
  rm -f ../$_chrome_bin
}
