# Maintainer: Doug Newgard <scimmia at archlinux dot org>

# Another maintainer: manuel (see forum.endeavouros.com)

# Chrome info:
_channel=stable
_chrome_ver=88.0.4324.96
_chrome_bin=google-chrome-${_channel}_${_chrome_ver}-1_amd64.deb

pkgname=vivaldi-widevine-now
pkgdesc='A browser plugin designed for the viewing of premium video content'
pkgver=4.10.1679.0
pkgrel=1
epoch=1
arch=('x86_64')
#_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
_url="https://raw.githubusercontent.com/manuel-192/m-more2/master/PKGBUILDs/$pkgname"
url='https://www.widevine.com/'
license=('custom')
provides=("vivaldi-widevine")
conflicts=('vivaldi-widevine' 'google-chrome')
options=('!strip')
source=(
  "https://dl.google.com/linux/deb/pool/main/g/google-chrome-${_channel}/${_chrome_bin}"
  $_url/update_chrome_version.sh
)
sha256sums=('4c3fca6ba917db4635368c99df61cd811c5138daa1ad890d4dc4271dd0014124'
            '318bee612a3a4f78657ace37f817b9346f7cc9c91e8a8d353b3a3dba74bc9d3c')
sha512sums=('6ee29f4ad5c8dcec70b1d18eb80e57fb4fd1b543f032e83bd50350bc8f903a7930a9f68c381d8659a235d310f3faa00bcae81e1e025135164648d2f48ed9f588'
            'c6bef828bb359e7fffa26fb534c5964014b1af444ed6f3222476e6248088607543bc4a76af423660fa01fa7a53508a77377a873869e7bfe84966693e9050661e')

prepare() {
  bsdtar -x --strip-components 4 -f data.tar.xz opt/google/chrome/WidevineCdm
}

pkgver() {
  grep '"version":' WidevineCdm/manifest.json | sed -e 's|^.*"version": "\([0-9\.]*\)",$|\1|'
}

package() {
  depends=('gcc-libs' 'glib2' 'glibc' 'nspr' 'nss')

  local targetdir=opt/google/chrome

  install -dm755 "$pkgdir/$targetdir"
  cp -a WidevineCdm "$pkgdir/$targetdir/"
  rm -f ../$_chrome_bin
}
