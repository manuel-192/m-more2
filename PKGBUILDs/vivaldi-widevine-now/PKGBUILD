# Maintainer: Doug Newgard <scimmia at archlinux dot org>

# Another maintainer: manuel (see forum.endeavouros.com)

# Chrome info:
_channel=stable
_chrome_ver=87.0.4280.141
_chrome_bin=google-chrome-${_channel}_${_chrome_ver}-1_amd64.deb

pkgname=vivaldi-widevine-now
pkgdesc='A browser plugin designed for the viewing of premium video content'
pkgver=4.10.1679.0
pkgrel=1
epoch=1
arch=('x86_64')
#_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
_url="https://raw.githubusercontent.com/manuel-192/m-more2/master/PKGBUILDs/$pkgname"
url='https://www.widevine.com/'
license=('custom')
provides=("vivaldi-widevine")
conflicts=('vivaldi-widevine' 'google-chrome')
options=('!strip')
source=(
  "https://dl.google.com/linux/deb/pool/main/g/google-chrome-${_channel}/${_chrome_bin}"
  $_url/update_chrome_version.sh
)
sha256sums=('b7edb7cd5c166bf3c0a1d245baa5924e242c3b81b97090468bec778f41f40373'
            'c6d0a452f24cad8d2027381ca7c74436f2f402a1d39a69b25cff5a44a22d03d7')
sha512sums=('35ae2181e10a0ba5d99fdb8d0b025ea792bfdd9b7bd4ea139ac292b6fbbea5be7f04b6f3b8044b82d4353b0be7578c39457048c80a03c24e74ed80a1a88a6338'
            'e74b2ae96c0032b1225382a4ab60fd8cdaa055d3845b53e7aa03704aa411fe99fe502cf900776b76c94de430b54c93a0655450d91775afe9d58a0f45780f06e1')

prepare() {
  bsdtar -x --strip-components 4 -f data.tar.xz opt/google/chrome/WidevineCdm
}

pkgver() {
  grep '"version":' WidevineCdm/manifest.json | sed -e 's|^.*"version": "\([0-9\.]*\)",$|\1|'
}

package() {
  depends=('gcc-libs' 'glib2' 'glibc' 'nspr' 'nss')

  local targetdir=opt/google/chrome

  install -dm755 "$pkgdir/$targetdir"
  cp -a WidevineCdm "$pkgdir/$targetdir/"
  rm -f ../$_chrome_bin
}
