# Maintainer: Doug Newgard <scimmia at archlinux dot org>

# Another maintainer: manuel (see forum.endeavouros.com)

# Chrome info:
_channel=stable
_chrome_ver=86.0.4240.183
_chrome_bin=google-chrome-${_channel}_${_chrome_ver}-1_amd64.deb

pkgname=vivaldi-widevine-now
pkgdesc='A browser plugin designed for the viewing of premium video content'
pkgver=4.10.1679.0
pkgrel=1
epoch=1
arch=('x86_64')
#_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
_url="https://raw.githubusercontent.com/manuel-192/m-more2/master/PKGBUILDs/$pkgname"
url='https://www.widevine.com/'
license=('custom')
provides=("vivaldi-widevine")
conflicts=('vivaldi-widevine' 'google-chrome')
options=('!strip')
source=(
  "https://dl.google.com/linux/deb/pool/main/g/google-chrome-${_channel}/${_chrome_bin}"
  $_url/update_chrome_version.sh
)
sha512sums=('9969c7206957821bb5a1cfc4db837f6127200ae1ed24b1242c1be9b3886cf7e0be612fb03f7d37d3fa02f9b74ccf94bac7bcaae06b38cd9cf7ae0ffbfc72ccf3'
            '57855e110cdbe45c4c045fcfb68a1cd1180f97ae5912bec6849ec96c89474d8798e572fbabc24ca21db828646fcee71044a867cbae6d21ebcb33aa23a5798c17')

prepare() {
  bsdtar -x --strip-components 4 -f data.tar.xz opt/google/chrome/WidevineCdm
}

pkgver() {
  grep '"version":' WidevineCdm/manifest.json | sed -e 's|^.*"version": "\([0-9\.]*\)",$|\1|'
}

package() {
  depends=('gcc-libs' 'glib2' 'glibc' 'nspr' 'nss')

  local targetdir=opt/google/chrome

  install -dm755 "$pkgdir/$targetdir"
  cp -a WidevineCdm "$pkgdir/$targetdir/"
  rm -f ../$_chrome_bin
}
