# Maintainer: Doug Newgard <scimmia at archlinux dot org>

# Another maintainer: manuel (see forum.endeavouros.com)

# Chrome info:
_channel=stable
_chrome_ver=87.0.4280.88
_chrome_bin=google-chrome-${_channel}_${_chrome_ver}-1_amd64.deb

pkgname=vivaldi-widevine-now
pkgdesc='A browser plugin designed for the viewing of premium video content'
pkgver=4.10.1679.0
pkgrel=1
epoch=1
arch=('x86_64')
#_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
_url="https://raw.githubusercontent.com/manuel-192/m-more2/master/PKGBUILDs/$pkgname"
url='https://www.widevine.com/'
license=('custom')
provides=("vivaldi-widevine")
conflicts=('vivaldi-widevine' 'google-chrome')
options=('!strip')
source=(
  "https://dl.google.com/linux/deb/pool/main/g/google-chrome-${_channel}/${_chrome_bin}"
  $_url/update_chrome_version.sh
)
sha256sums=('4bc68502dc5e22d5c264b3fb5d0a1ecfdadc3ba21d945646d8d37de4b8a96e58'
            'c6d0a452f24cad8d2027381ca7c74436f2f402a1d39a69b25cff5a44a22d03d7')
sha512sums=('19eea1d1be171cab60ce5135572da9388b4b72e313118478b53f65c0bf2293733809282736b98ef828a208b7426e5191258f8c666cba7510b8bf5c92d0010a47'
            'e74b2ae96c0032b1225382a4ab60fd8cdaa055d3845b53e7aa03704aa411fe99fe502cf900776b76c94de430b54c93a0655450d91775afe9d58a0f45780f06e1')

prepare() {
  bsdtar -x --strip-components 4 -f data.tar.xz opt/google/chrome/WidevineCdm
}

pkgver() {
  grep '"version":' WidevineCdm/manifest.json | sed -e 's|^.*"version": "\([0-9\.]*\)",$|\1|'
}

package() {
  depends=('gcc-libs' 'glib2' 'glibc' 'nspr' 'nss')

  local targetdir=opt/google/chrome

  install -dm755 "$pkgdir/$targetdir"
  cp -a WidevineCdm "$pkgdir/$targetdir/"
  rm -f ../$_chrome_bin
}
