# Maintainer: Doug Newgard <scimmia at archlinux dot org>

# More up to date by manuel (see forum.endeavouros.com)

pkgname=vivaldi-widevine-now
pkgdesc='A browser plugin designed for the viewing of premium video content'
pkgver=4.10.1679.0
_chrome_ver=86.0.4240.183
_chrome_bin=google-chrome-stable_${_chrome_ver}-1_amd64.deb
pkgrel=1
epoch=1
arch=('x86_64')
url='https://www.widevine.com/'
license=('custom')
provides=("chromium-widevine-dev=$pkgver")
conflicts=('vivaldi-widevine' 'google-chrome')
options=('!strip')
source=("https://dl.google.com/linux/deb/pool/main/g/google-chrome-stable/$_chrome_bin")
sha256sums=('c9d37f91449ea3563d518b13164c5aefb42d166d5aec91f9832948d335ee0de4')

prepare() {
  bsdtar -x --strip-components 4 -f data.tar.xz opt/google/chrome/WidevineCdm
}

pkgver() {
  awk 'match($0,/"version": "([0-9.]*)"/,a) {print a[1];}' WidevineCdm/manifest.json
}

package() {
  depends=('gcc-libs' 'glib2' 'glibc' 'nspr' 'nss')

  local targetdir=opt/google/chrome

  install -dm755 "$pkgdir/$targetdir"
  cp -a WidevineCdm "$pkgdir/$targetdir/"
  rm -f ../$_chrome_bin

  # ln -s /$targetdir/WidevineCdm "$pkgdir/usr/lib/chromium/WidevineCdm"
  # ln -s WidevineCdm/_platform_specific/linux_x64/libwidevinecdm.so "$pkgdir/usr/lib/chromium/libwidevinecdm.so"
}
